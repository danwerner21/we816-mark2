Name     GAL-U13 ;
PartNo   00 ;
Date     8/9/2024 ;
Revision 04 ;
Designer John Coffman ;
Company  Coffman Consulting ;
Assembly None ;
Location  ;
Device   g22v10 ;

/* *************** INPUT PINS *********************/
PIN [2..11] = [a10..19] ; /* Upper 9 address*/
PIN   1   = a21         ; /*                */
PIN   13  = a22         ; /*                                 */
PIN   14  = a20         ; /*                                 */
PIN   23  = a23         ; /*                                 */
PIN   21  = a8          ; /*                                 */
PIN   22  = a9          ; /*                                 */



/* *************** OUTPUT PINS *********************/
PIN   15  = !cs_rom     ; /*                                 */
PIN   16  = !cs_ram     ; /*                                 */
PIN   17  = !io         ; /*                                 */
PIN   18  = !fbus       ; /*                                 */
PIN   19  = !o_rom      ; /*                                 */


/* ****  MEMORY MAP ******

000000..00DFFF	LOW RAM < 32K
00E000..00FFFF  LOW ROM
010000..07FFFF	HIGH RAM > 64K
080000..FEFFFF	EXTERNAL ECB BUS RAM
FF0000..FFFFFF OPTION ROM

00FE00..00FEFF	I/O AREA -- FIXED; DOES NOT DEPEND ON ROM SIZE
************************ */

z1	=	!a23 & !a22 & !a21 & !a20			;
z0	=	!a19 & !a18 & !a17 & !a16			;

hi_ram	=	z1 & !a19 & (a18 # a17 # a16)		;	/* 010000..07FFFF */

rom_area	=  a15 &  a14 & a13	;

io_area	=  a15 & a14 & a13 & a12 & a11 & a10 & a9 & !a8		;

cs_ram	=	z1 & z0 & !rom_area   #
		hi_ram			;

cs_rom	=	z1 & z0 & rom_area & !io		;
o_rom	=	a23 & a22 & a21 & a20;

io	=	z1 & z0 & io_area		;

/*fbus =  !(cs_rom # cs_ram # io) ; */
fbus	=	!z1 # a19					;
